group 'com.lange.trader'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
}

project.ext {
    v = [
            cofoja: '1.3-20160207',
            guava: '19.0',
            commonsLang3: "3.1",

            junit: '4.11',
            assertJ: '3.3.0'
    ]

    outputPath = sourceSets.main.output.classesDir
    cofoja = files("lib/cofoja.asm-${v.cofoja}.jar")

    outputContractsDir = files('build/contracts') {
        builtBy('compileContracts')
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile cofoja
    compile "com.google.guava:guava:${v.guava}"
    compile "org.apache.commons:commons-lang3:${v.commonsLang3}"

    runtime "com.google.guava:guava:${v.guava}"
    runtime "org.apache.commons:commons-lang3:${v.commonsLang3}"

    testCompile "junit:junit:${v.junit}"
    testCompile "org.assertj:assertj-core:${v.assertJ}"
    testCompile outputContractsDir

}

compileJava {
    options.compilerArgs = ['-proc:none']
}

task compileContracts(type: JavaCompile) {
    source = sourceSets.main.java.srcDirs
    classpath = configurations.compile
    destinationDir = outputContractsDir as File
    options.compilerArgs = ['-processor',
                            'com.google.java.contract.core.apt.AnnotationProcessor',
                            '-proc:only']
}

test {
    jvmArgs "-javaagent:lib/cofoja.asm-${v.cofoja}.jar"
}

mainClassName = "com.lange.trader.main.Repl"

jar {
    manifest {
        attributes 'Main-Class': mainClassName,
                'Class-Path': configurations.runtime.files.collect {"$it.name"}.join(' ')
    }
}
